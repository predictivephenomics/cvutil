# copy_runtime_deps.cmake
#
# This script is invoked at build time via cmake -P.
# It expects the following variables to be defined:
#   - TARGET_FILE: the full path to the built target (e.g. an .exe or .dll)
#   - INSTALL_BIN: the directory where the target is located (for copying dependencies)
#
# It uses file(GET_RUNTIME_DEPENDENCIES) (available in CMake 3.21+) to determine
# the runtime dependencies of the target and then copies each dependency into the INSTALL_BIN folder.

if(NOT DEFINED TARGET_FILE)
  message(FATAL_ERROR "TARGET_FILE not defined")
endif()

if(NOT DEFINED INSTALL_BIN)
  message(FATAL_ERROR "INSTALL_BIN not defined")
endif()

# Get runtime dependencies for the given target file.
file(GET_RUNTIME_DEPENDENCIES
  LIBRARIES "${TARGET_FILE}"
  EXECUTABLES "${TARGET_FILE}"
  DIRECTORIES ${SEARCH_DIRS}
  RESOLVED_DEPENDENCIES_VAR deps
  UNRESOLVED_DEPENDENCIES_VAR unresolved
)

message(STATUS "Scanning runtime dependencies for: ${TARGET_FILE}")
message(STATUS "Found dependencies:")

# Create an empty file first to ensure it exists
file(WRITE "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "# Runtime dependencies for cvutil\n")
file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "# Generated by copy_runtime_deps.cmake\n\n")
file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "if(NOT DEFINED CVUTIL_RUNTIME_DEPENDENCIES)\n")
file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "  set(CVUTIL_RUNTIME_DEPENDENCIES \"\")\n")
file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "endif()\n\n")

foreach(dep IN LISTS deps)
  #message(STATUS "  ${dep}")
  # Extract the file name from the full path.
  if(dep MATCHES "^[cC]:[\\\\/][wW][iI][nN][dD][oO][wW][sS][\\\\/][sS]ystem32")
    #message(STATUS "Skipping system dependency: ${dep}")
  else()
    get_filename_component(dep_name "${dep}" NAME)
    set(dest "${INSTALL_BIN}/${dep_name}")
    if(NOT EXISTS "${dest}")
      file(INSTALL "${dep}" DESTINATION "${INSTALL_BIN}")
      message(STATUS "Copied ${dep} to ${INSTALL_BIN}")
    else()
      message(STATUS "Already exists: ${dest}")
    endif()
    
    # # # Track this dependency for later installation
    # set_property(GLOBAL APPEND PROPERTY RUNTIME_DEPENDENCY_FILES "${dep}")
    # # Also create a cache variable that can be exported to external projects
    # set(CVUTIL_RUNTIME_DEPENDENCIES "${CVUTIL_RUNTIME_DEPENDENCIES};${dep}" CACHE INTERNAL "Runtime dependencies for cvutil")
    # # In your library's CMakeLists.txt or Config.cmake.in file
    # set(CVUTIL_RUNTIME_DEPENDENCIES ${CVUTIL_RUNTIME_DEPENDENCIES} CACHE INTERNAL "Runtime dependencies for CVUtil library")

    # # Export the variable in your <LibraryName>Config.cmake file
    # set(CVUTIL_RUNTIME_DEPENDENCIES "${CVUTIL_RUNTIME_DEPENDENCIES}" CACHE STRING "Runtime dependencies for CVUtil")
    
    # Replace \\ with / in ${deps}
    string(REPLACE "\\" "/" dep "${dep}")
    string(REPLACE "//" "/" dep "${dep}")
    
    # Add the dependency to a list file that will be installed
    file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" "set(CVUTIL_RUNTIME_DEPENDENCIES \"\${CVUTIL_RUNTIME_DEPENDENCIES};\${CMAKE_CURRENT_LIST_DIR}/../../../bin/${dep_name}\")\n")
    # # Add the dependency to the list file that will be installed
    # file(APPEND "${CMAKE_BINARY_DIR}/cvutil_runtime_dependencies.cmake" 
    #      "list(APPEND CVUTIL_RUNTIME_DEPENDENCIES \"${dep}\")\n")
  endif()
endforeach()

# Process unresolved dependencies.
if(unresolved)
  message(WARNING "There are unresolved dependencies:")
  foreach(dep IN LISTS unresolved)
    # message(STATUS "  ${dep}")
    get_filename_component(dep_name "${dep}" NAME)
    # If the dependency appears to be a system DLL (using common prefixes), ignore it.
    string(REGEX MATCH "^(api-ms-win|ext-ms-win|ext-ms-onecore|ext-ms-mf)" _ignore "${dep_name}")
    if(_ignore)
      # message(STATUS "Ignoring system dependency: ${dep_name}")
    else()
      message(STATUS "Unresolved dependency: ${dep}")
    endif()
  endforeach()
endif()

# Add any additional runtime dependencies that weren't auto-detected
# This is passed as EXTRA_RUNTIME_DEPS variable
if(DEFINED EXTRA_RUNTIME_DEPS)
  foreach(extra_dep IN LISTS EXTRA_RUNTIME_DEPS)
    if(EXISTS "${extra_dep}")
      get_filename_component(dep_name "${extra_dep}" NAME)
      set(dest "${INSTALL_BIN}/${dep_name}")
      if(NOT EXISTS "${dest}")
        file(INSTALL "${extra_dep}" DESTINATION "${INSTALL_BIN}")
        message(STATUS "Copied extra dependency: ${dep_name}")
      else()
        message(STATUS "Extra dependency already exists: ${dep_name}")
      endif()
      # Add to the runtime dependencies list
      file(APPEND "${BINARY_DIR}/cvutil_runtime_dependencies-${BUILD_CONFIG}.cmake" 
           "set(CVUTIL_RUNTIME_DEPENDENCIES \"\${CVUTIL_RUNTIME_DEPENDENCIES};\${CMAKE_CURRENT_LIST_DIR}/../../../bin/${dep_name}\")\n")
    endif()
  endforeach()
endif()
